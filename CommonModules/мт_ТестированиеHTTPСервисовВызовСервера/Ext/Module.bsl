///////////////////////////////////////////////////////////////////////////////////////////////////////
// (с) Tolkachev Pavel, 2020
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает полный шаблон URL HTTP-сервиса. Полный шаблон строится из корневого URL HTTP-сервиса и URL шаблона
// Например, если корневой URL HTTP-сервиса - "dt", а шаблон URL - "/storage/{Storage}/{ID}", то будет возвращено
// значение "dt/storage/{Storage}/{ID}". Если шаблон отсутствует, то будет вызвано Исключение.
//
// Параметры:
//  ПолноеИмяШаблона - Строка - полное имя шаблона URL. Указывается по правилу "<ИмяHttpСервиса>.<ИмяШаблонаUrl>".
//                              Например, для получения шаблона URL "Получить" HTTP-сервиса "ПередачаДанных" необходимо
//                              передать строку "ПередачаДанных.Получить".
// 
// Возвращаемое значение:
//  Строка - полный шаблон URL HTTP сервиса.
//
Функция ПолучитьШаблонUrlHttpСервиса(ПолноеИмяШаблона) Экспорт
	
	СоставИмениШаблона = СтрРазделить(ПолноеИмяШаблона, ".");
	
	Если СоставИмениШаблона.Количество() <> 2 Тогда
		ВызватьИсключение НСтр("ru='Неверное полное имя шаблона. '") +
			НСтр("ru='Имя должно быть в формате <ИмяHttpСервиса>.<ИмяШаблонаUrl>.'");
	КонецЕсли; 
	
	ИмяHttpСервиса = СоставИмениШаблона[0];
	ИмяШаблонаUrl = СоставИмениШаблона[1];
	
	HttpСервис = Метаданные.HTTPСервисы.Найти(ИмяHttpСервиса);
	Если HttpСервис = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='HTTP-сервис %1 отсутствует'"), ИмяHttpСервиса);
	КонецЕсли; 
	
	ШаблонUrl = HttpСервис.ШаблоныURL.Найти(ИмяШаблонаUrl);
	Если ШаблонUrl = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru='Отсутствует шаблон URL %1 HTTP-сервиса %2'"),
			ИмяШаблонаUrl, ИмяHttpСервиса);
	КонецЕсли; 
	
	Возврат HttpСервис.КорневойURL + ШаблонUrl.Шаблон;
	
КонецФункции 

#КонецОбласти